plugins {
    id "com.github.johnrengelman.shadow" version "7.0.0" apply false
}

/** Credentials for repo.zalupario.ru */
def user = findProperty("repo.zalupa.user")
def pass = findProperty("repo.zalupa.pass")

// Хихихиха)
def isWinston = true

ext.set('output', findProperty("gradle.output") ?: "output/")

allprojects {
    group = "dev.pkgh"
    version = "1.0.0-Dev"
}

configure(subprojects) {
    apply plugin: "java"
    apply plugin: "java-library"
    apply plugin: "com.github.johnrengelman.shadow"

    repositories {
        mavenCentral()

        if (isWinston) {
            maven {
                url = "https://repo.zalupario.ru/repository/maven-releases/"

                credentials {
                    username = user
                    password = pass
                }

                mavenContent {
                    releasesOnly()
                }
            }
        } else {
            mavenLocal()
        }
    }

    tasks {
        shadowJar {
            exclude 'xyz/winston/impl/compiletime/**'   // impls
            exclude 'META-INF/DEPENDENCIES'
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/NOTICE.txt'
            exclude 'META-INF/maven/**'

            mergeServiceFiles {
                include 'META-INF/impl/**'              // impls
            }
        }

        compileJava {
            targetCompatibility = JavaVersion.VERSION_17
            sourceCompatibility = JavaVersion.VERSION_17
            options.encoding = "UTF-8"
        }

        compileTestJava {
            targetCompatibility = JavaVersion.VERSION_17
            sourceCompatibility = JavaVersion.VERSION_17
            options.encoding = "UTF-8"
        }

        build {
            dependsOn(shadowJar)
        }

        test {
            useJUnitPlatform()
        }
    }

    dependencies {
        // Lombok annotations
        compileOnly "org.projectlombok:lombok:1.18.20"
        annotationProcessor "org.projectlombok:lombok:1.18.20"
        testCompileOnly "org.projectlombok:lombok:1.18.20"
        testAnnotationProcessor "org.projectlombok:lombok:1.18.20"

        // JetBrains annotations
        compileOnly "org.jetbrains:annotations:20.1.0"

        // Unit-Tests
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }
}